=== play_es ===

= 2_to_6_es
¡Vamos! Recuerda, mi objetivo es llegar a la salida, ¡y tratar de evitar las paredes!
-> end_level_check(3) ->
Ey, {get_user_name()}, ¿sabías que en verdad Riley creó estos niveles? # character: ada
-> mid_level_check(4) ->
Bueno, no lo hice todo... Tú me ayudaste con el diseño, Ada. Y Saniel me ayudó con el código, y Felix--
-> mid_level_check(4) ->
¡No seas tan dura contigo misma, Riley! Se necesita mucha habilidad para armar algo como esto. # character: faber
-> mid_level_check(4) ->
¡Así es! Riley hizo esto como uno de sus proyectos para La academia. Ella era la responsable del proyecto y debía administrarlo por su cuenta. Pero cualquiera que estuviera cerca podía ayudarla. ¡Creo que es fácil ver por qué es nuestra estudiante estrella! # character: ada
-> mid_level_check(4) ->
Aw, vamos ... no le des tanta importancia, es vergonzoso -_- ;;
Um, ¡concentrémonos en superar este nivel, {get_user_name()}!
-> end_level_check(4) ->
¡Oh, oh! <b>Hoyos</b>, ¿eh? Me pregunto cómo los cruzarás... # character: saniel
-> mid_level_check(5) ->
¡Necesitas usar la instrucción <b><i>Jump</i></b> (saltar) para cruzar los <b>hoyos</b>! Aunque hay un truco. <b><i>Jump</i></b> puede cruzar un <b>hoyo</b> sólo si está <b>directamente</b> frente a tí.
-> end_level_check(5) -> end_level_check(6) -> 6_to_10_es

= 6_to_10_es
Por cierto, esos hoyos no fueron nada fáciles de dibujar... es difícil obtener el efecto de profundidad con tan pocos colores.
-> mid_level_check(7) ->
- Riley, lo has hecho extremadamente bien. ¡Ojalá todos los estudiantes tuvieran tu talento! # character: ada
-> end_level_check(7) ->
- Ey Riley, creo que tu juego tiene algunos <b><i>bugs</i></b> (errores), ¡Ja, ja! Qué genial grupo de robots. <tt>Beep. Boop. Hola. Faber. Yo. Soy. Un. Bugbot.</tt> # character: faber
-> mid_level_check(8) ->
Sabía que te gustarían, Faber :D
-> mid_level_check(8) ->
¡Mantente alerta, {get_user_name()}! Los <i>bugbots</i> se mueven cuando tú lo haces, así que deberás planear tus movimientos con cuidado. # character: estelle
Observa cómo los <i>bugbots</i> se mueven y en qué dirección. Y piensa en los espacios que dejan libres luego de moverse. # character: estelle
-> mid_level_check(8) ->
Estos <i>bugbots</i> <b>sólo se mueven hacia abajo</b>, nunca hacia la izquierda o derecha. Eso podría permitirte encontrar un... <i>bug</i> (error) en su comportamiento. ^_^ # character: estelle
Oh, claro. Cuando se salen del cuadro por abajo, ¡reaparecen por arriba! ¡Así que pon atención a eso!
-> end_level_check(8) -> end_level_check(9) -> end_level_check(10) -> 10_to_14_es

= 10_to_14_es
¡Hora de nuevos <i>bugbots</i>! Los de este tipo <b>sólo se mueven hacia arriba</b>, así como los del inicio <b>sólo se mueven hacia abajo</b>. ¡Presta atanción hacia dónde miran!
-> end_level_check(11) -> end_level_check(12) -> end_level_check(13) ->
<i>Bugbots</i>, hoyos... Riley, has hecho un gran trabajo aquí. # character: ada
-> end_level_check(14) ->
<b><i>mjau ^ = ^ mjav \/\/\\\\ ngiyaw \/\\ ^  miav, miau!</i></b> # character: felix
~ controlsCutscene = 1
* [(wait for: controlsCutscene is 0)] (¿¿??)
- ¿Qué fue <b>eso</b>? ¿Riley? # character: faber
-> 15_to_18_es

= 15_to_18_es
-> mid_level_check(15) ->
¡Creo que tenemos un problema! Riley, parece que los controles han desaparecido. ¿Alguna idea? # character: ada
-> mid_level_check(15) ->
¡Me estaba preguntando cuándo haría su aparición Felix!
Por primera vez, llega a tiempo y hace exactamente lo que le pedí. ¡<b> Ahora </b> las cosas se pondrán interesantes!
-> mid_level_check(15) ->
Mira {get_user_name()}, hay un conjunto de instrucciones ya confeccionadas, dispuestas ahí. # character: estelle
-> mid_level_check(15) ->
Y ahora, ¡entramos al modo difícil! ¿Ves ese botón de <b><i>Play</i></b> a la izquierda? Intenta presionarlo.
-> end_level_check(15) ->
En este modo, tienes que planear todos los movimientos del nivel por adelantado. Arrastra las instrucciones al lugar que desees. Luego presiona <b><i>Play</i></b> para comenzar nuestro viaje por el laberinto. Si lo piensas bien, pasarás estos niveles en tiempo récord.
-> end_level_check(16) ->
Hmm, ¡no hay forma de terminar este nivel con las instrucciones en este orden! Deberás reordenarlas para pasar. # character: saniel
-> mid_level_check(17) ->
Arrastra y suelta las instrucciones para reordenarlas, ¡y presiona <b><i>Play</i></b> para correr el nivel! Y por cierto, una vez que presiones <b><i>Play</i></b>... ¡no intentes reordenar las instrucciones hasta que pierdas o ganes!
-> end_level_check(17) ->
Fantástico trabajo. Prácticamente has navegado por ese nivel, {get_user_name()}. ¡Excelente planificación! # character: saniel
-> mid_level_check(18) -> 18_to_22_es

= 18_to_22_es
Riley, puedo ver la progresión desde la resolución de acertijos hasta la planificación estratégica en este juego, y estoy impresionado. # character: saniel
-> mid_level_check(18) ->
Guau, gra-gracias, Saniel! Eso significa mucho para mí.
-> end_level_check(18) ->
Ey Riley, ¿cómo va tu laberinto? # character: faber
-> mid_level_check(19) ->
Es bastante... ¡asombroso! :D En serio {get_user_name()}, espero que te estés divirtiendo tanto como nosotros. ¡Es tan guay ver jugar a alguien a la cosa que ayudé a construir!
-> mid_level_check(19) ->
¡Aja! Mira a tus instrucciones cuidadosamente. Tienes un solo salto, así que úsalo bien. Podrías necesitar esquivar los hoyos de alguna otra forma. # character: ada
-> end_level_check(19) ->
Subamos la temperatura. Ahora tienes dos saltos.
-> mid_level_check(20) ->
Este nivel puede parecer fácil, pero ahora tienes una sola instrucción adelante. Creo que hay un solo lugar en este nivel en el que absolutamente <b>debes</b> moverte hacia adelante, y no arriba o abajo. ¿Puedes encontrarlo? # character: ada
-> end_level_check(20) ->
Diría que algo acerca de este nivel me <b>baja</b> el ánimo, ¡pero este juego es muy guay justamente por eso! Aún así, ¿cómo se supone que nos movamos hacia adelante cuando lo único que se puede hacer es bajar? ¡No nos queda otra alternativa que intentarlo! # character: faber
-> end_level_check(21) ->
Ya lo sabes, no es necesario que haya un hoyo frente a tí para usar la instrucción <b>salto</b>. ¿Tal vez podrías usar un <b>salto</b> para ir adelante? ¡Nunca se sabe cuándo esto puede ser útil! # character: ada
-> end_level_check(22) ->
Has navegado a lo largo de todos esos niveles. Por lo tanto, voy a darte una recompensa.
~ set_game_state("quest.Sidetrack1/complete", true)
~ set_game_state("quests.achievements/sidetrack1-complete", true)
* ❯
  -> hack_es
